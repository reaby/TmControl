// #RequireContext CServerPlugin
declare Ident[Text] G_UILayers;

Boolean LayerExists(Text _LayerName) {
	if (!G_UILayers.existskey(_LayerName)) return False;
	if (!UIManager.UILayers.existskey(G_UILayers[_LayerName])) return False;
	return True;
}

CUILayer LayerGet(Text _LayerName) {
	if (!LayerExists(_LayerName)) return Null;
	return UIManager.UILayers[G_UILayers[_LayerName]];
}

Void Delete(Text _LayerName) {
	if (!LayerExists(_LayerName)) return;
	UIManager.UILayerDestroy(UIManager.UILayers[G_UILayers[_LayerName]]);
	G_UILayers.removekey(_LayerName);
}

CUILayer Create(Text _LayerName, Text _LayerManialink) {
	if (LayerExists(_LayerName)) Delete(_LayerName);
	declare CUILayer NewLayer <=> UIManager.UILayerCreate();
	G_UILayers[_LayerName] = NewLayer.Id;
	if (NewLayer != Null) {
		NewLayer.ManialinkPage = _LayerManialink;
		NewLayer.IsVisible = True;
	}
	return NewLayer;
}

Void Show(Text _LayerName, CClient _Client) {
	declare CUILayer Layer <=> LayerGet(_LayerName);
	Layer.IsVisible = True;

	if (Layer == Null) return;

	if (_Client != Null) {
		declare CUIConfig UI <=> UIManager.GetUI(_Client);
		if (UI == Null) return;
		if (!UI.UILayers.exists(Layer)) UI.UILayers.add(Layer);
	} else if (!UIManager.UIAll.UILayers.exists(Layer)) {
		UIManager.UIAll.UILayers.add(Layer);
	}
}

Void Show(Text _LayerName) {
	Show(_LayerName, Null);
}

Void Hide(Text _LayerName, CClient _Client) {
	declare CUILayer Layer <=> LayerGet(_LayerName);
	if (Layer == Null) return;

	if (_Client == Null) {
		UIManager.UIAll.UILayers.removekey(Layer.Id);
	} else {
		declare CUIConfig UI <=> UIManager.GetUI(_Client);
		if (UI == Null) return;
		UI.UILayers.removekey(Layer.Id);
	}
}

Void Hide(Text _LayerName) {
	Hide(_LayerName, Null);
}
