// #RequireContext CServerPlugin
#Include "TmControl/Libs/Ui.Script.txt" as UI
#Include "TextLib" as TL
#Include "MathLib" as ML

/**
Text ModeCallbackType Trackmania.Event.WayPoint
Text[] ModeCallbackData [{
	"time": 21450260,
	"login": "",
	"accountid": "",
	"racetime": 1,
	"laptime": 1,
	"checkpointinrace": 0,
	"checkpointinlap": 0,
	"isendrace": false,
	"isendlap": false,
	"isindependentlaps": false,
	"curracecheckpoints": [19450],
	"curlapcheckpoints": [19450],
	"blockid": "#50331649",
	"speed": 54.71880
}]
*/


#Struct K_WayPoint {
	Integer time;
	Text login;
	Real speed;
	Integer laptime;
	Integer checkpointinlap;
}

#Struct K_CpInfo {
	Integer index;
	Text trigram;
	Integer time;
	Integer speed;
}

#Const C_widgetMaxCp 21

declare K_CpInfo[] BestCpsData;

Text getManialink() {
	declare Integer maxColumns = 6;
	declare Integer maxRows = 2;
	declare Integer y = 89;
	declare Integer index = 0;

	declare Text out = """
	<?xml version="1.0" encoding="utf-8" standalone="yes"?>
	<manialink version="3" name="TmControl.Widget.BestCps">
	<stylesheet>
	<style class="center" halign="left" valign="center2" textsize="1" />
	</stylesheet>
	""";

	for (row, 0, maxRows) {
		for (column, 0, maxColumns) {
			out ^= """
			<frame pos="{{{-130 + (column*37)}}} {{{ y- (row*5) }}}" id="cp_{{{index}}}" hidden="1">
			<label pos="2 -2" size="4 4" halign="center" text="" class="center" textsize="1" />
			<label pos="5 -2" size="12 4" text=""  class="center" textsize="0.5" />
			<label pos="17.7 -2" size="19 4" text="" class="center" textsize="1" />
			<!-- <label pos="30 -2" size="12 4" text="" textsize="0.8"  class="center"/> -->
			<quad pos="0 0" size="4 4" bgcolor="0007" halign="left" z-index="-2"/>
			<quad pos="4.5 0" size="12 4" bgcolor="0004" halign="left" z-index="-2"/>
			<quad pos="17 0" size="19 4" bgcolor="0004" halign="left" z-index="-2"/>

			</frame>""";
			index +=1;
		}
	}
	out ^= """
	<script><!--
	#Include "MathLib" as ML
	#Include "TextLib" as TL
	#Struct K_CpInfo {
		Integer index;
		Text trigram;
		Integer time;
		Integer speed;
	}

	declare K_CpInfo[] G_BestCps;

	Void updateWidgetData() {
		foreach (data in G_BestCps) {
			declare CMlFrame frame <=> (Page.GetFirstChild("cp_"^data.index) as CMlFrame);
			if (frame != Null) {
				if (data.time == -1) {
					frame.Hide();
				}
				if (data.time > -1) {
					(frame.Controls[0] as CMlLabel).Value = ""^(data.index+1);
					(frame.Controls[1] as CMlLabel).Value = TL::TimeToText(data.time, True, True);
					(frame.Controls[2] as CMlLabel).Value = data.trigram;
					//(frame.Controls[3] as CMlLabel).Value = data.speed^"km/h";
					frame.Show();
				}
			}
		}
	}

	main() {
		declare Text tmcontrol_bestcps_data for LocalUser = "[]";
		declare Integer tmcontrol_bestcps_update for LocalUser = 0;
		declare Integer LastUpdate = 0;
		LastUpdate = 0;
		tmcontrol_bestcps_update = 0;

		while (True) {
			yield;
			if (tmcontrol_bestcps_update != LastUpdate) {
				LastUpdate = tmcontrol_bestcps_update;
				G_BestCps.fromjson(TL::Replace(tmcontrol_bestcps_data, "'", "\""));
				updateWidgetData();
			}
		}
	}
	--></script>
	""";
	out ^= """</manialink>""";

	return out;
}

Void Update() {
	declare Text out = BestCpsData.tojson();
	out = TL::Replace(out, "\"", "'");

	declare Text xml = """
	<manialink version="3" name="TmControl.Widget.BestCpsUpdater">
	<script><!--
	main() {
		declare Integer tmcontrol_bestcps_update for LocalUser;
		declare Text tmcontrol_bestcps_data for LocalUser = "[]";
		tmcontrol_bestcps_data = "{{{ out }}}";
		tmcontrol_bestcps_update = {{{ Now }}};
	}
	--></script>
	</manialink>
	""";

	UI::Create("BestCpsUpdater", xml);
	UI::Show("BestCpsUpdater");
}

Void Reset() {
	BestCpsData.clear();
	for(i, 0, C_widgetMaxCp) {
		BestCpsData.add(K_CpInfo{index = i, time = -1});
	}
	Update();
}

Void Init() {
	UI::Create("WidgetBestCps", getManialink());
	UI::Show("WidgetBestCps");
	Reset();
}

Void OnModeEvent(CServerPluginEvent Event) {
	log(Event);
	switch (Event.ModeCallbackType) {
		case "Maniaplanet.Podium_Start": {
			Reset();
		}

		case "Maniaplanet.StartMap_End": {
			Reset();
		}

		case "Trackmania.Event.WayPoint": {
			declare K_WayPoint waypoint;
			waypoint.fromjson(Event.ModeCallbackData[0]);
			declare Integer i = waypoint.checkpointinlap;
			if (waypoint.checkpointinlap <= C_widgetMaxCp) {
				if (BestCpsData.existskey(i)) {
					if (BestCpsData[i].time == -1 || waypoint.laptime < BestCpsData[i].time) {
						declare CUser User <=> GetClient(waypoint.login).User;
						declare K_CpInfo cp = K_CpInfo {
							index = waypoint.checkpointinlap,
							speed = ML::NearestInteger(waypoint.speed*3.6),
							trigram = User.Name,
							time = waypoint.laptime
						};
						BestCpsData[waypoint.checkpointinlap] = cp;
						Update();
					}
				}
			}
		}
	}
}