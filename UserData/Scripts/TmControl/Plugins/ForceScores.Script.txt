// #RequireContext CServerPlugin
#Include "TmControl/Libs/File.Script.txt" as File
#Include "TmControl/Libs/Ui.Script.txt" as UI
#Include "TextLib" as TL

#Struct K_PlayerData {
	Text login;
	Text name;
	Integer rank;
	Integer roundpoints;
	Integer mappoints;
	Integer matchpoints;
}

#Struct K_RoundPoints {
	Text responseid;
	K_PlayerData[] players;
}

#Struct K_JsonPlayer {
	Text Login;
	Integer Points;
}


declare Text G_PointsListXml;

Void Init() {
	G_PointsListXml = File::Read("TmControl/UI/Admin/forceScores.xml");
}

Text GenPlayerList(K_RoundPoints _Points) {
	declare Text Out = "";
	declare Integer Idx = 1;

	foreach (Player in _Points.players) {
		Out ^= """
		<frame pos="0 -{{{(Idx-1)*6}}}" size="156 5" id="{{{ Player.login }}}">
		<label pos="1 -4.5"   size="12 4" text="{{{ Player.rank }}}" textsize="2" valign="bottom"/>
		<label pos="11 -4.5"  size="20 4" text="{{{ Player.login}}}" textsize="2" valign="bottom"/>
		<label pos="32 -4.5"  size="51 4" text="{{{ Player.name }}}" textsize="2" valign="bottom"/>
		<entry pos="83 -4.5"  size="51 4" default="{{{ Player.matchpoints }}}" textsize="2" valign="bottom" selecttext="1"/>
		</frame>""";
		Idx +=1;
	}
	return Out;
}

Void ShowPointsAdmin(CClient _Client) {
	TriggerModeScriptEvent("Trackmania.GetScores", [_Client.User.Login]);
}

Void HidePointsAdmin(CClient _Client) {
	UI::Hide("ForcePointsPanel", _Client);
}

Void OnUiEvent(CUIConfigEvent _UIEvent) {
	declare Text[] Data = _UIEvent.CustomEventData;
	if (Data.count >= 2) {
		declare Text Action = Data[0];
		declare CClient Client = GetClient(_UIEvent.UI);
		if (Client != Null) {
			switch (Action) {
				case "Close": {
					HidePointsAdmin(GetClient(_UIEvent.UI));
					return;
				}
				case "Apply": {
					declare K_JsonPlayer[] JsonPlayer;
					JsonPlayer.fromjson(Data[1]);
					foreach (Player in JsonPlayer) {
						TriggerModeScriptEvent("Trackmania.SetPlayerPoints", [Player.Login, "0","0","""{{{Player.Points}}}"""]);
					}
					HidePointsAdmin(GetClient(_UIEvent.UI));
					return;
				}
			}
		}
	}
}

Void OnModeEvent(CServerPluginEvent _Event) {
	switch (_Event.ModeCallbackType) {
		case "Trackmania.Scores": {

			declare K_RoundPoints Points;
			Points.fromjson(_Event.ModeCallbackData[0]);
			declare Text Xml = G_PointsListXml;
			Xml = TL::Replace(Xml, "<!--%players%-->", GenPlayerList(Points));

			UI::Create("ForcePointsPanel", Xml);
			declare CClient Client = GetClient(Points.responseid);
			if (Client != Null) {
				UI::Show("ForcePointsPanel", Client);
			}
		}
	}
}