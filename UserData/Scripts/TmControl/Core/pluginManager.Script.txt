// #RequireContext CServerPlugin
#Include "TextLib" as TL
#Include "TMControl/Core/config.Script.txt" as Config
#Include "TMControl/Core/serverManager.Script.txt" as Server
#Include "TMControl/Plugins/ServerAdmin.Script.txt" as ServerAdmin
#Include "TMControl/Plugins/Players.Script.txt" as PlayersPanel
#Include "TMControl/Plugins/Maps.Script.txt" as MapsPanel
#Include "TMControl/Plugins/BestCheckpoints.Script.txt" as WidgetBestCheckpoints
#Include "TMControl/Plugins/ForceScores.Script.txt" as ForceRoundPoints

*** OnInit ***
*** 
// this should get run, but never does.
log("PluginManager: OnInit");
***

Void Init() {     
    +++ OnInit +++
    PlayersPanel::Init();
    ServerAdmin::Init();
    WidgetBestCheckpoints::Init();
    ForceRoundPoints::Init();
}

Void OnModeReady() {
    Server::onModeReady();
    
    foreach (Client in Players) {
        if (Config::isAdmin(Client)) {
            ServerAdmin::SendAdminPanel(Client);
        } else {
            ServerAdmin::SendPlayerPanel(Client);
        }                
    }
    
    foreach (Client in Spectators) {
        if (Config::isAdmin(Client)) {
            ServerAdmin::SendAdminPanel(Client);
        }
        else {
            ServerAdmin::SendPlayerPanel(Client);
        }
    }
}

Void HandleEvent(CServerPluginEvent Event) { 
    declare CClient Client <=> Event.Client;
    declare Boolean isAdmin = False;
    declare Text PlayerLevel = "Player";
    if (Client != Null) {
        isAdmin = Config::isAdmin(Client);
        if (isAdmin) PlayerLevel = "Admin";
    }
    
    switch (Event.Type) {
        case CServerPluginEvent::EType::ClientDisconnected:  {
            if (Client != Null) {
                Server::SendChat("""$ada{{{ PlayerLevel }}} $<$fff{{{ Client.User.Name }}}$> leaves""");
            }
        }
        
        case CServerPluginEvent::EType::ClientConnected: {     
            if (Client != Null) {
                Server::SendChat("""$ada{{{ PlayerLevel }}} $<$fff{{{ Client.User.Name }}}$> joins""");   
            }
            if (isAdmin) {
                ServerAdmin::SendAdminPanel(Client);
            } else {
                ServerAdmin::SendPlayerPanel(Client);
            }
        }
        
        case CServerPluginEvent::EType::ChatCommand: {   
            Server::SendChat("Available commands:", [Client]);
            Server::SendChat("$ada/help $fff- show help", [Client]);
            if (isAdmin) {                       
                ServerAdmin::OnChat(Client, Event.ChatCommandType, Event.ChatCommandData);
            }            
            
        }
        
        case CServerPluginEvent::EType::ModeCallback: {
            WidgetBestCheckpoints::OnModeEvent(Event);
            ForceRoundPoints::OnModeEvent(Event);
            // log(Event);
        }
        
        default: {  
            // log(Event);
        }
    }
}

Void HandleUiEvent(CUIConfigEvent UiEvent) {
    
    if (UiEvent.CustomEventType == "TmControl.Server.AddAction") {
        declare Text[] data = UiEvent.CustomEventData;
        if (data.count >= 1) {
            declare Text action = data[0];
            data.removekey(0);
            Server::_AddAction(action,data);    
        } 
    }
    
    if (UiEvent.CustomEventType == "TmControl.Plugins.ServerAdmin") {
        declare Text data = UiEvent.CustomEventData[0];
        declare CClient Client <=> GetClient(UiEvent.UI);
        if (Config::isAdmin(Client)) {
            switch (data) {
                case "!Players": {
                    PlayersPanel::ShowPlayerAdmin(Client);
                }
                case "!MapList": {
                    MapsPanel::ShowMaps(Client);
                }
                case "!Config": {
                    Server::_AddAction("!Config", []);
                }
                case "!ForceScores": {
                    ForceRoundPoints::ShowPointsAdmin(Client);
                }
                default: {}
            }  
        }    
    }
    
    if (UiEvent.CustomEventType == "TmControl.Plugins.Players") {
        PlayersPanel::OnUiEvent(UiEvent);
    }
    
    if (UiEvent.CustomEventType == "TmControl.Plugins.Maps") {
        MapsPanel::OnUiEvent(UiEvent);
    }
    
    if (UiEvent.CustomEventType == "TmControl.Plugins.ForceScores") {
        ForceRoundPoints::OnUiEvent(UiEvent);
    }
    
}


Void onMainLoop() {
    
}